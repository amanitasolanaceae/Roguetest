import abs, clamp, floor, random from math
import 'kernel' as Kernel
class Game
    frametime: 1/60
    offset: 0
    title: 'Yet Another Stupid Roguelike'
    size: w: 80, h: 25
    new: =>
        global GAME = @
        with terminal
            .open!
            .set"window: title='#{@title}', size=#{floor @size.w}x#{floor @size.h};"
            .set"font: #{@font.face}, size=#{@font.size};" if @font
            .set"input.filter = [keyboard, mouse+]"
        @prepare!
        @run!
    prepare: => @kernel = Kernel!
    run: =>
        --make the clock have its curTime before starting
        clock\step!
        frametimer = 0
        terminal.refresh!
        while true
            dt = clock\step!
            for v in *{1/2, 1, 2}
                v *= @frametime
                dt = v if abs(dt - v) < .002
            dt = clamp dt, 0, @frametime*2
            frametimer += dt
            frametimer = clamp frametimer, 0, @frametime*8
            ticked = false
            while frametimer > @frametime
                frametimer -= @frametime
                @update @frametime
                ticked = true
            if ticked
                @render frametimer / @frametime 
                terminal.refresh!
            sleep .001
    update: (...) => @kernel\update ...
    render: (...) => @kernel\render ...